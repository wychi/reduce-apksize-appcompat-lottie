apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "cms.appcompatmin"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        String finalBuildName = variant.name.capitalize()
        def processResTask = tasks.findByName("process${finalBuildName}Resources");

        // NOTE: remove files will cause AAPT fail. (replace instead)
        // replace unwanted layout before aapt-rules.txt is generated.
        processResTask.doFirst {
            def dummyLayout = file("$projectDir/buildconf/dummy_layout.xml");
            def UNNEDDED_FILE_LIST = "$projectDir/buildconf/UNNEEDED_IMAGE_RESOURCE_FILES.txt"
            File unneededFileList = new File(UNNEDDED_FILE_LIST)
            if (unneededFileList.exists()) {
                unneededFileList.eachLine { line ->
                    if (!line.startsWith("#") && line.trim()) {

                        def unneededRes = file("${buildDir}/intermediates/res/merged/${variant.flavorName}/${variant.buildType.name}/${line}")
                        if(!unneededRes.exists()) {
                            println "mismatch. update UNNEEDED_IMAGE_RESOURCE_FILES accordingly. ${line}"
                            return
                        }

                        if(line.endsWith(".xml")) {
                            def input = dummyLayout.newDataInputStream()
                            def output = unneededRes.newDataOutputStream()
                            output << input
                            input.close()
                            output.close()
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
//        exclude group: 'com.android.support', module: 'support-annotations'
//    })
    compile 'com.android.support:appcompat-v7:25.3.0'
//    testCompile 'junit:junit:4.12'
}
